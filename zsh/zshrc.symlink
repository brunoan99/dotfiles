# shortcyut to this dotfiles path is $DOT
export DOT=$HOME/.dotfiles
export CONFIG=$HOME/.config

# project folder that we can `c [tab] to`
export PROJECTS=~/workspace/js/alucar

# Stash environment variables in ~/.localrc. This means they'll stay out
# of main dotfiles repository (which may be public, like this one), but
# you'll have access to them in scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of zsh files
typeset -U config_files
config_files=($DOT/**/*.zsh)

for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

export STARSHIP_CONFIG=$DOT/zsh/starship.toml

eval "$(starship init zsh)"
